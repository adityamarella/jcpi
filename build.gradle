allprojects {
	apply plugin: 'base'
	
	group = 'jcpi'
	
	repositories {
		mavenCentral()
	}
}

subprojects {
	configurations {
		eclipselibs {
			extendsFrom compile
		}
	}

	task libs(type: Sync) {
		from configurations.eclipselibs
		into "$projectDir/lib"
	}
}

configure(rootProject) {
	apply plugin: 'java'
	
	description = 'Java Chess Protocol Interface'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	dependencies {
		testCompile 'junit:junit:4.+'
	}
	
	jar {
		manifest {
			attributes 'Implementation-Title': project.group, 'Implementation-Version': project.version
		}
	}
	
	task dist(type: Zip) {
		def baseDir = "${project.name}-${project.version}"
		
		into ("$baseDir") {
			from 'README.md'
			from 'LICENSE'
			from 'NOTICE'
			from jar
		}
		into ("$baseDir/example") {
			from 'src/example'
		}
	}

	artifacts {
		archives dist
	}
	
	task wrapper(type: Wrapper) {
		gradleVersion = '1.5'
	}
}

def downloadFile(srcUrl, destUrl) {
	def dest = new BufferedOutputStream(new FileOutputStream(destUrl))
	dest << new URL(srcUrl).openStream()
	dest.close()
}
