apply plugin: 'java'

description = 'Java Chess Protocol Interface'

group = 'jcpi'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenCentral()
}

configurations {
	eclipse {
		extendsFrom compile
	}
}

dependencies {
	testCompile 'junit:junit:4.+'
}

jar {
    manifest {
        attributes 'Implementation-Title': group, 'Implementation-Version': version
    }
}

task libs(type: Sync) {
	from configurations.eclipse
	into "$projectDir/lib"
}

task dist(type: Zip) {
	def baseDir = "${project.name}-${project.version}"
	
	into ("$baseDir") {
		from 'README.md'
		from 'LICENSE'
		from 'NOTICE'
		from jar
	}
	into ("$baseDir/example") {
		from 'src/example'
	}
}

artifacts {
	archives dist
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.2'
}
